
@매크로 설정창열기:
	pass
	:설정창열기
	
	[시스템사운드 아이디="설정창사운드" 파일명="btn09.ogg"]
	
	[이전터치막기 아이디="시스템설정창터치막기"]
	
	[이미지 아이디="시스템설정창배경" 파일명="옵션/background.png" GUI="예" x=640 y=1020]
	[애니메이션 아이디="시스템설정창배경" 타입="이동" 위치="640,360" 시간="1" 가속="백아웃"  ]
	[슬라이더 아이디="문장속도슬라이더" 읽기전용="아니오" x=150 y=20.0 값=$문장속도 GUI="예" 부모="시스템설정창배경"]
	[슬라이더 아이디="효과음크기슬라이더" 읽기전용="아니오" x=0 y=-100 값=$효과음크기 GUI="예" 부모="시스템설정창배경"]
	[슬라이더 아이디="시스템음량슬라이더" 읽기전용="아니오" x=190 y=-100 값=$시스템음량 GUI="예" 부모="시스템설정창배경"]
	[슬라이더 아이디="배경음크기슬라이더" 읽기전용="아니오" x=-190 y=-100 값=$배경음크기 GUI="예" 부모="시스템설정창배경"]
	[슬라이더 아이디="자동읽기속도슬라이더" 읽기전용="아니오" x=150 y=90 값=$자동읽기속도 GUI="예" 부모="시스템설정창배경"]
	
	@조건 $전체화면여부=="예" :
		[전체화면예]
	@그외:
		[전체화면아니오]
	
	[이미지 아이디="확인이미지" 파일명="옵션/button_big.png" x=-90.0 y=160.0 북마크이동="설정창저장" GUI="예" 부모="시스템설정창배경"]
	[이미지 아이디="취소이미지" 파일명="옵션/button_big.png" x=85.0 y=160.0 북마크이동="설정창닫기" GUI="예" 부모="시스템설정창배경"]
	[텍스트 내용="확인" 아이디="확인버튼" 크기=40.0 x=0 y=0 GUI="예" 부모="확인이미지"]
	[텍스트 내용="취소" 아이디="취소버튼" 크기=40.0 x=0 y=0 GUI="예" 부모="취소이미지"]
	
	[키입력해제 아이디="키입력감지" ]
	[빨리감기 허용="아니오"]
	

@매크로 설정창저장:
	pass
	:설정창저장
	
	$문장속도=[슬라이더값 아이디="문장속도슬라이더"]
	$효과음크기=[슬라이더값 아이디="효과음크기슬라이더"]
	$시스템음량=[슬라이더값 아이디="시스템음량슬라이더"]
	$배경음크기=[슬라이더값 아이디="배경음크기슬라이더"]
	$자동읽기속도=[슬라이더값 아이디="자동읽기속도슬라이더"]
	
	@조건 전체화면:
		$전체화면여부="예"
	@그외:
		$전체화면여부="아니오"
	
	[옵션적용]
	
	[설정창닫기]

@매크로 설정창닫기:
	pass
	:설정창닫기
	
	[시스템사운드 아이디="설정창사운드" 파일명="btn09.ogg"]
	[삭제 아이디="취소버튼"]
	[삭제 아이디="확인버튼"]
	[삭제 아이디="취소이미지"]
	[삭제 아이디="확인이미지"]
	[삭제 아이디="전체화면아니오텍스트"]
	[삭제 아이디="전체화면예텍스트"]
	[삭제 아이디="전체화면아니오이미지"]
	[삭제 아이디="전체화면예이미지"]
	[삭제 아이디="자동읽기속도슬라이더"]
	[삭제 아이디="배경음크기슬라이더"]
	[삭제 아이디="시스템음량슬라이더"]
	[삭제 아이디="효과음크기슬라이더"]
	[삭제 아이디="문장속도슬라이더"]
	[삭제 아이디="시스템설정창배경"]
	[삭제 아이디="시스템설정창터치막기"]
	[키입력 매크로="백로그매크로" 아이디="키입력감지"]
	[빨리감기 허용="예"]

@매크로 전체화면예:
	pass
	@조건 거짓:
		pass
		:전체화면예
		[시스템사운드 아이디="설정창사운드" 파일명="btn09.ogg"]
	[이미지 아이디="전체화면예이미지" 파일명="옵션/button.png" x=-205 y=40 북마크이동="전체화면예" GUI="예" 부모="시스템설정창배경"]
	[이미지 아이디="전체화면아니오이미지" 파일명="옵션/button_disable.png" x=-95 y=40.0 북마크이동="전체화면아니오" GUI="예" 부모="시스템설정창배경"]
	[텍스트 내용="예" 아이디="전체화면예텍스트" x=0 y=0 GUI="예" 부모="전체화면예이미지"]
	[텍스트 내용="아니오" 아이디="전체화면아니오텍스트" x=0 y=0 GUI="예" 부모="전체화면아니오이미지"]
	전체화면=참

@매크로 전체화면아니오:
	pass
	@조건 거짓:
		pass
		:전체화면아니오
		[시스템사운드 아이디="설정창사운드" 파일명="btn09.ogg"]
	[이미지 아이디="전체화면예이미지" 파일명="옵션/button_disable.png" x=-205 y=40 북마크이동="전체화면예" GUI="예" 부모="시스템설정창배경"]
	[이미지 아이디="전체화면아니오이미지" 파일명="옵션/button.png" x=-95 y=40.0 북마크이동="전체화면아니오" GUI="예" 부모="시스템설정창배경"]
	[텍스트 내용="예" 아이디="전체화면예텍스트" x=0 y=0 GUI="예" 부모="전체화면예이미지"]
	[텍스트 내용="아니오" 아이디="전체화면아니오텍스트" x=0 y=0 GUI="예" 부모="전체화면아니오이미지"]
	전체화면=거짓

@매크로 SFX:
	[효과음 아이디=SFX.아이디 파일명=SFX.파일명 볼륨=$효과음크기 * 0.01]

@매크로 시스템사운드:
	[효과음 아이디=시스템사운드.아이디 파일명=시스템사운드.파일명 볼륨=$시스템음량 * 0.01]

@매크로 옵션적용:
	pass
	문장속도=[변수 아이디="$문장속도"]
	효과음크기=[변수 아이디="$효과음크기"]
	시스템음량=[변수 아이디="$시스템음량"]
	배경음크기=[변수 아이디="$배경음크기"]
	자동읽기속도=[변수 아이디="$자동읽기속도"]
	전체화면여부=[변수 아이디="$전체화면여부"]
	
	[대사창수정 아이디="대화" 시간=(100-문장속도)*0.005]
	[대사창수정 아이디="독백" 시간=(100-문장속도)*0.005]
	
	[배경음볼륨 볼륨=배경음크기*0.01]
	
	[전체화면전환 전체화면=전체화면여부]

#초기 설정
초기화여부=[변수 아이디="$초기화여부"]

@조건 초기화여부:
	[옵션적용]
@그외:
	$문장속도 = 50
	$효과음크기 = 50
	$시스템음량 = 50
	$배경음크기 = 50
	$자동읽기속도 = 50
	$전체화면여부 = 거짓
	$초기화여부 = 참
	

[이미지 아이디="설정버튼" 파일명="optionButton.png" 위치="오른쪽상단" 북마크이동="설정창열기" GUI="예"]
